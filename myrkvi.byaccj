/*
*	Author: 	Snorri Agnarsson 
*				Tumi Snær Gíslason
*	
*	Byacc/J Parser for Myrkvi
*
*	Compilation(example with y.output file):
*
*		byaccj -vJ -Jclass=MyrkviParser myrkvi.byaccj
*		javac MyrkviParser.java MyrkviParserVal.java
*
*/

%{
 	import java.io.*;
 	import java.util.*;	
%}

%token<sval> LITERAL, NAME, OPNAME, PRINT
%token IF,ELIF,ELSE,WHILE,DEF,RETURN,VAR,PRINT,NOTOP,ANDOP,OROP,
%right PRINT,RETURN, ANDOP,OROP
%left '=',OPNAME,NOTOP

%%

start
	:		{ emit("\""+name+".mexe\" = main in"); }
			{ emit("!{{"); }
		program
			{ emit("}}*BASIS;"); }
	;

program
	:	program function
	|	function
	;

function
	:	DEF NAME '(' 				
			{ varCount = 0; }
			{ varTable = new HashMap<String,Integer>(); }	
		optnames ')' 				
			{ addVar($2, optVarTable); }
			{ emit("#\""+$2+"[f"+varCount+"]\" = "); }
			{ emit("["); }
			{ emit("(MakeVal null)"); }
		body						
			{ emit("(Return)"); }
			{ emit("];"); }
	;


optnames
	:	/* nothing */	
	|	dummynames	
	;

dummynames
	:	dummynames ',' NAME 		
			{ addVar($3, varTable); }
	
	|	NAME		 				
			{ addVar($1, varTable); }	
	;

names
	:	names ',' NAME 				
			{ addVar($3, varTable); }
			{ emit("(Push)"); }
	
	|	NAME 						
			{ addVar($1, varTable); }
			{ emit("(Push)"); }
	
	|	NAME						
			{ addVar($1, varTable); }
			{ emit("(Push)"); }
		'=' expr					
			{ emit("(Store "+findVar($1, varTable)+")"); }			
	;

decls
	:	/* nothing */
	| 	decl ';' decls
	;

decl
	: 	VAR names
	;


exprs
	:	expr ';' exprs
	|	expr ';'
	; 

expr
	:	NAME 						
			{ emit("(Fetch "+findVar($1, varTable)+")"); }
	
	|	NAME '=' expr 				
			{ emit("(Store "+findVar($1, varTable)+")"); }
	
	|	NAME '(' optargs ')'		
			{ optVarCount = findVar($1, optVarTable); }
			{ emit("(Call #\""+$1+"[f"+optVarCount+"]\" "+optVarCount+")"); }

	|	PRINT expr					
			{ emit("(Call #\"writeln[f1]\" 1)"); }
	
	|	RETURN expr 				
			{ emit("(Return)"); }

	|	OPNAME expr 				
			{ emit("(Call #\""+$1+"[f1]\" 1)"); }

	|	expr OPNAME 				
			{ emit("(Push)"); } 
		expr 						
			{ emit("(Call #\""+$2+"[f2]\" 2)"); }
									
	|	LITERAL						
			{ emit("(MakeVal "+$1+")"); }			
	
	|	'(' expr ')'
	
	|	ifexpr						

	|	NOTOP expr 					
			{ emit("(Not)"); }

	|	expr ANDOP					
			{ emit("(Push)"); }
			{ emit("(GoFalse _and"+ ++boolCount +")"); }
		expr						
			{ emit("_and"+boolCount+":"); }
	
	|	expr OROP 					
			{ emit("(Push)"); }
			{ emit("(GoTrue _or"+ ++boolCount +")"); }
		expr						
			{ emit("_or"+boolCount+":"); }

	|	WHILE 						
			{ emit("_whileCond:"); }
		expr          				
			{ emit("(Push)"); }
			{ emit("(GoFalse _whileEnd)"); }
		body						
			{ emit("(Go _whileCond)"); }
			{ emit("_whileEnd:"); }
									
	;


optargs
	:	/* nothing */
	|	args
	;

args
	:	args 						
			{ emit("(Push)"); }
		',' expr 				
	
	|	expr 						
	;


ifexpr
	:	IF expr  					
			{ emit("(Push)"); }
			{ emit("(GoFalse _"+ ++elifCount +")"); }
		body  						
			{ emit("(Go _endIf)"); }
			{ emit("_"+ elifCount +":"); }
		elifexpr elseexpr			
			{ emit("_endIf:"); }
			{ elifCount = 0; }
									
									
	;

elifexpr
	:	/* nothing */
	|	ELIF expr 					
			{ emit("(Push)"); }
			{ emit("(GoFalse _elif"+ ++elifCount +")"); }
		body 						
			{ emit("(Go _endIf)"); }
			{ emit("_elif"+ elifCount +":"); }
		elifexpr
	;

elseexpr
	:	ELSE body 			
	|	/* nothing */
	;

body
	:	'{' decls exprs '}'
	;





%%


	static private String name;
	private MyrkviLexer lexer;
	private int varCount;
	private int boolCount = 0;
	private int optVarCount;
	private HashMap<String,Integer> varTable;
	private HashMap<String,Integer> optVarTable = new HashMap<String,Integer>();
	private int elifCount = 0;

	private void addVar( String name , HashMap<String,Integer> table)
	{
		if( table.get(name) != null )
			yyerror("Variable/function "+name+" already exists");
		if(table == varTable)
			table.put(name,varCount++);
		else if(table == optVarTable)
			table.put(name,varCount);
	}

	private int findVar( String name , HashMap<String,Integer> table)
	{
		Integer res = table.get(name);
		if( res == null )
			yyerror("Variable/function "+name+" does not exist");
		return res;
	}


	int last_token_read;

	private int yylex()
	{
		int yyl_return = -1;
		try
		{
			yylval = null;
			last_token_read = yyl_return = lexer.yylex();
			//System.out.println(MyrkviParser.yyname[yyl_return]);
			if( yylval==null )
				yylval = new MyrkviParserVal(MyrkviParser.yyname[yyl_return]);
		}
		catch (IOException e)
		{
			System.err.println("IO error: "+e);
		}
		return yyl_return;
	}


	public void yyerror( String error ){
		System.out.println("Error:  "+error);
		System.out.println("Token:  "+MyrkviParser.yyname[last_token_read]);
		System.exit(1);
	}

	public MyrkviParser( Reader r )
	{
		lexer = new MyrkviLexer(r,this);
	}

	public static void main( String args[] )
	  	throws IOException
	{
		MyrkviParser yyparser = new MyrkviParser(new FileReader(args[0]));
		name = args[0].substring(0,args[0].lastIndexOf('.'));
		yyparser.yyparse();
	}

	public static void emit( String s )
	{
		System.out.println(s);
	}